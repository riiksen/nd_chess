require "crypto/bcrypt/password"

class User < Crecto::Model
  include Crypto

  schema "users" do
    field :email, String
    field :hashed_password, String
  end

  validate_required [:email, :hashed_password]
  unique_constraint :email

  def password=(password)
    @new_password = password
    @hashed_password = Bcrypt::Password.create(password, cost: 10).to_s
  end

  def password
    (hash = hashed_password) ? Bcrypt::Password.new(hash) : nil
  end

  def password_changed?
    new_password ? true : false
  end

  def valid_password_size?
    (pass = new_password) ? pass.size >= 8 : false
  end

  def authenticate(password : String)
    (bcrypt_pass = self.password) ? bcrypt_pass == password : false
  end

  private getter new_password : String?
end
